plugins {
    id 'org.springframework.boot' version '2.2.2.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'java'
}

test {
    useJUnitPlatform()
    testLogging {
        events("passed", "skipped", "failed")
    }
}

subprojects {
    group = 'me.nexters'
    version = '0.0.1-SNAPSHOT'

    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    sourceCompatibility = '11'

    task initSourceFolders {
        sourceSets*.java.srcDirs*.each {
            if( !it.exists() ) {
                it.mkdirs()
            }
        }

        sourceSets*.resources.srcDirs*.each {
            if( !it.exists() ) {
                it.mkdirs()
            }
        }
    }

    repositories {
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Greenwich.SR3'
        }
    }

    configurations {
        developmentOnly
        runtimeClasspath {
            extendsFrom developmentOnly
        }
        compileOnly {
            extendsFrom annotationProcessor
        }
        compile.exclude group: "org.springframework.boot", module: "spring-boot-starter-logging"
    }

    // TODO domain 빼고 나머지 분리해야됨
    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-log4j2'
        implementation 'com.playtika.reactivefeign:feign-reactor-webclient:1.0.45'
        implementation 'com.playtika.reactivefeign:feign-reactor-spring-configuration:1.0.45'
        implementation 'org.springframework.boot:spring-boot-starter-webflux'
        implementation 'org.apache.poi:poi-ooxml:3.15'
        implementation 'org.springframework.boot:spring-boot-starter-data-mongodb-reactive'

        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testCompileOnly 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'junit', module: 'junit'
        }
        testImplementation 'org.junit.jupiter:junit-jupiter-engine'
        testImplementation 'org.junit.jupiter:junit-jupiter-params'
        testImplementation 'com.googlecode.json-simple:json-simple:1.1.1'
    }
}

project(':todoc-batch') {
    dependencies {
        compile project (':todoc-domain')
    }
}

project(':todoc-api') {
    dependencies {
        compile project (':todoc-domain')
    }
}

